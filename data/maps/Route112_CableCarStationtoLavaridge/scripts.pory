
raw`Route112_CableCarStationtoLavaridge_MapScripts::
    map_script MAP_SCRIPT_ON_TRANSITION, Route112_CableCarStationtoLavaridge_OnTransition
    map_script MAP_SCRIPT_ON_FRAME_TABLE, Route112_CableCarStationtoLavaridge_OnFrame
    .byte 0

Route112_CableCarStationtoLavaridge_OnTransition:
    setescapewarp MAP_ROUTE112, 28, 28
    call_if_eq VAR_UNUSED_0x40A8, 2, Route112_CableCarStationToLavaridge_EventScript_MoveAttendantAside
    end

Route112_CableCarStationToLavaridge_EventScript_MoveAttendantAside::
    setobjectxyperm 2, 7, 4
    setobjectmovementtype 2, MOVEMENT_TYPE_FACE_LEFT
    return

Route112_CableCarStationtoLavaridge_OnFrame:
    map_script_2 VAR_UNUSED_0x40A8, 2, Route112_CableCarStationToLavaridge_EventScript_ExitCableCar
    .2byte 0`


script Route112_CableCarStationToLavaridge_EventScript_ExitCableCar{
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Route112_CableCarStationToLavaridge_Movement_ExitCableCar) 
	applymovement(2, Route112_CableCarStationToLavaridge_Movement_FollowPlayerOutFromCableCar) 
	waitmovement(0)
	setvar(VAR_UNUSED_0x40A8, 0) 
	setobjectxyperm(2, 6, 7) 
	setobjectmovementtype(2, MOVEMENT_TYPE_FACE_DOWN) 
	releaseall
	end 
	
}

script Route112_CableCarStationToLavaridge_EventScript_Attendant{
	lock
	faceplayer
	msgbox("The CABLE CAR is ready to go up.\nWould you like to be on it?",MSGBOX_YESNO)
        if(var(VAR_RESULT) == TRUE){
            goto(Route112_CableCarStationToLavaridge_EventScript_RideCableCar)
        }else{
            goto(Route112_CableCarStationToLavaridge_EventScript_DeclineRide)
        }
	end
}

script Route112_CableCarStationToLavaridge_EventScript_DeclineRide {
    msgbox("Please ride with us another time.", MSGBOX_DEFAULT)
	release
    end
}

script Route112_CableCarStationToLavaridge_EventScript_RideCableCar{
	msgbox("Please step this way.", MSGBOX_DEFAULT) 
	closemessage
	applymovement(2, Route112_CableCarStationToLavaridge_Movement_LeadPlayerToCableCar) 
	applymovement(OBJ_EVENT_ID_PLAYER, Route112_CableCarStationToLavaridge_Movement_BoardCableCar) 
	waitmovement(0)
	setvar(VAR_0x8004, FALSE)
	setvar(VAR_UNUSED_0x40A8, 1)
	incrementgamestat(GAME_STAT_RODE_CABLE_CAR)
	special(CableCarWarpBis)
	special(CableCar)
	waitstate
	release
	end 
    
}

movement Route112_CableCarStationToLavaridge_Movement_LeadPlayerToCableCar {
	walk_up
	walk_up
	walk_right
	walk_in_place_faster_left
	step_end
}

movement Route112_CableCarStationToLavaridge_Movement_FollowPlayerOutFromCableCar{
	delay_16
	walk_left
	walk_down
	walk_down
	step_end 
	
}

movement Route112_CableCarStationToLavaridge_Movement_BoardCableCar{
	walk_up
	walk_up
	walk_up
	delay_16
	step_end 
}

movement Route112_CableCarStationToLavaridge_Movement_ExitCableCar{
	walk_down
	walk_down
	walk_down
	delay_16
	step_end 
	
}